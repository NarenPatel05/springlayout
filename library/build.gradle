apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.coderoller"
archivesBaseName = "springlayout"

signing {
  required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    version "0.9.1"
    compileSdkVersion 18
    buildToolsVersion "20.0.0"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}


uploadArchives {
  configuration = configurations.archives
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: sonatypeRepo) {
      authentication(userName: sonatypeUsername,
          password: sonatypePassword)
    }

    pom.project {
      name 'SpringLayout'
      packaging 'aar'
      description 'RelativeLayout on steroids.'
      url 'https://github.com/sulewicz/springlayout'

      scm {
        url 'scm:git@github.com:sulewicz/springlayout.git'
        connection 'scm:git@github.com:sulewicz/springlayout.git'
        developerConnection 'scm:git@github.com:sulewicz/springlayout.git'
      }

      licenses {
        license {
          name 'The MIT License'
          url 'https://raw.githubusercontent.com/sulewicz/springlayout/master/LICENSE'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'sulewicz'
          name 'Szymon Ulewicz'
          email 'szymon.ulewicz@coderoller.org'
        }
      }
    }
  }
}